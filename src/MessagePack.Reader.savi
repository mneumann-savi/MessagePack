:class MessagePack.Reader
  :let _rd MessagePack.Token.Reader

  :new (_bytes)
    @_rd = MessagePack.Token.Reader.new(_bytes)

  :fun box is_eos: @_rd.is_exhausted

  :fun ref read_nil! None
    @_rd.next!.nil!

  :fun ref read_bool! Bool
    @_rd.next!.bool!

  :fun ref read_uint! U64
    @_rd.next!.u64_convertible!

  :fun ref read_int! I64
    @_rd.next!.i64_convertible!

  :fun ref read_f64! F64
    @_rd.next!.f64_convertible!

  :fun ref read_string! String
    len = @_rd.next!.string_ahead!
    --@_rd.read_string!(len)

  :fun ref read_binary! Bytes
    len = @_rd.next!.binary_ahead!
    @_rd.read_bytes!(len)

  :fun ref read_array_head! USize
    @_rd.next!.array_ahead!.usize

  :fun ref read_map_head! USize
    @_rd.next!.map_ahead!.usize

  :fun ref read_ext! Pair(I8, Bytes)
    ext = @_rd.next!.ext_ahead!
    type = ext.hi, len = ext.lo

    Pair(I8, Bytes).new(type, @_rd.read_bytes!(len))

  :fun ref read_timestamp! Pair(I64, U64)
    ext = @_rd.next!.ext_ahead!
    type = ext.hi
    len = ext.lo
    error! if (type != -1)

    case len == (
    // timestamp 32 stores the number of seconds that have elapsed since 1970-01-01 00:00:00 UTC
    // in an 32-bit unsigned integer:
    // +--------+--------+--------+--------+--------+--------+
    // |  0xd6  |   -1   |   seconds in 32-bit unsigned int  |
    // +--------+--------+--------+--------+--------+--------+
    | 4 |
      secs = @_rd.read_native_u32!.be_to_native.i64
      Pair(I64, U64).new(secs, 0)

    // timestamp 64 stores the number of seconds and nanoseconds that have elapsed since
    // 1970-01-01 00:00:00 UTC in 32-bit unsigned integers:
    // +--------+--------+--------+--------+--------+------|-+--------+--------+--------+--------+
    // |  0xd7  |   -1   | nanosec. in 30-bit unsigned int |   seconds in 34-bit unsigned int    |
    // +--------+--------+--------+--------+--------+------^-+--------+--------+--------+--------+
    | 8 |
      data = @_rd.read_native_u64!.be_to_native.u64
      nanos = data.bit_shr(34), secs = data.bit_and(0x00000003ffffffff).i64
      Pair(I64, U64).new(secs, nanos)

    // timestamp 96 stores the number of seconds and nanoseconds that have elapsed since
    // 1970-01-01 00:00:00 UTC in 64-bit signed integer and 32-bit unsigned integer:
    // +--------+--------+--------+--------+--------+--------+--------+
    // |  0xc7  |   12   |   -1   |nanoseconds in 32-bit unsigned int |
    // +--------+--------+--------+--------+--------+--------+--------+
    // +--------+--------+--------+--------+--------+--------+--------+--------+
    //                     seconds in 64-bit signed int                        |
    // +--------+--------+--------+--------+--------+--------+--------+--------+
    | 12 |
      nanos = @_rd.read_native_u32!.be_to_native.u64
      secs  = @_rd.read_native_u64!.be_to_native.i64
      Pair(I64, U64).new(secs, nanos)

    | error!
    )

