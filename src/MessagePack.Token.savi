:: A poor-man's tagged union.
::
:: Field `_value` is interpreted differently depending on `type`.
:struct val MessagePack.Token
  :let type MessagePack.Token.Type
  // `size` is only used when decoding MessagePack data. It is the number of bytes
  // that represents the token, excluding following payload data.
  :let size U8
  :let _value U64

  :new (@type, @_value, @size = U8[0])

  :fun non nil: @new(MessagePack.Token.Type.Nil, 0)

  :fun non bool(b Bool): @new(MessagePack.Token.Type.Bool, if b (1 | 0))

  :fun non uint(u U64): @new(MessagePack.Token.Type.UInt, u)

  :fun non int(i I64): @new(MessagePack.Token.Type.Int, i.u64)

  :fun non float(f F64): @new(MessagePack.Token.Type.Float, f.bits)

  :fun non string_ahead(len U32): @new(MessagePack.Token.Type.StringAhead, len.u64)

  :fun non binary_ahead(len U32): @new(MessagePack.Token.Type.BinaryAhead, len.u64)

  :fun non array_ahead(len U32): @new(MessagePack.Token.Type.ArrayAhead, len.u64)

  :fun non map_ahead(len U32): @new(MessagePack.Token.Type.MapAhead, len.u64)

  :fun non ext_ahead(type I8, len U32)
    @new(
      MessagePack.Token.Type.ExtAhead
      type.u8.u64.bit_shl(32).bit_or(len.u64)
    )

  :fun sized(size U8): @new(@type, @_value, size)

  :fun nil!
    case @type == (
    | MessagePack.Token.Type.Nil | None
    |                              error!
    )

  :fun bool! Bool
    case @type == (
    | MessagePack.Token.Type.Bool | @_value != 0
    |                               error!
    )

  :fun u64_convertible! U64
    case @type == (
    | MessagePack.Token.Type.UInt  | @_value
    | MessagePack.Token.Type.Int   | @_value.i64.u64!
    | MessagePack.Token.Type.Float | F64.from_bits(@_value).u64!
    |                                error!
    )

  :fun i64_convertible! I64
    case @type == (
    | MessagePack.Token.Type.UInt  | @_value.i64!
    | MessagePack.Token.Type.Int   | @_value.i64
    | MessagePack.Token.Type.Float | F64.from_bits(@_value).i64!
    |                                error!
    )

  :fun f64_convertible! F64
    case @type == (
    | MessagePack.Token.Type.UInt  | @_value.f64!
    | MessagePack.Token.Type.Int   | @_value.i64.f64!
    | MessagePack.Token.Type.Float | F64.from_bits(@_value)
    |                                error!
    )

  :fun string_ahead! U32
    case @type == (
    | MessagePack.Token.Type.StringAhead | @_value.u32
    |                                      error!
    )

  :fun binary_ahead! U32
    case @type == (
    | MessagePack.Token.Type.BinaryAhead | @_value.u32
    |                                      error!
    )

  :fun array_ahead! U32
    case @type == (
    | MessagePack.Token.Type.ArrayAhead | @_value.u32
    |                                     error!
    )

  :fun map_ahead! U32
    case @type == (
    | MessagePack.Token.Type.MapAhead | @_value.u32
    |                                   error!
    )

  :fun ext_ahead! Pair(I8, U32)
    case @type == (
    | MessagePack.Token.Type.ExtAhead |
      type = @_value.bit_shr(32).u8.i8
      len = @_value.bit_and(0xFFFF_FFFF).u32
      Pair(I8, U32).new(type, len)
    | error!
    )

:: Actual data is stored in the fields `_hi` and `_lo` of `MessagePack.Token`. 
::
:enum MessagePack.Token.Type
  :member Nil 0

  :: Truthiness stored in `_value`
  :member Bool 1

  :: U64 value stored in `_value`
  :member UInt 2

  :: I64 value stored in `_value`
  :member Int 3

  :: F64 value stored in `_value`
  :member Float 4

  :: String length stored in lower 32-bits of `_value`.
  :member StringAhead 5

  :: Binary length stored in lower 32-bits of `_value`.
  :member BinaryAhead 6

  :: Array length stored in lower 32-bits of `_value`.
  :member ArrayAhead 7

  :: Map length stored in lower 32-bits of `_value`.
  :member MapAhead 8

  :: Ext data length stored in lower 32-bits of `_value`.
  :: Ext type stored in in higher 32-bits of `_value`.
  :member ExtAhead 9

